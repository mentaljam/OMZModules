####################### CPack options ######################

function(configure_cpack)

    message(STATUS "For building packages use \"cpack -G <GENERATOR_NAME>\"")

    #### Version
    if(NOT DEFINED CPACK_PACKAGE_VERSION_MAJOR)
        set(CPACK_PACKAGE_VERSION_MAJOR ${PROJECT_VERSION_MAJOR} PARENT_SCOPE)
        set(CPACK_PACKAGE_VERSION_MAJOR ${PROJECT_VERSION_MAJOR})
    endif()
    if(NOT DEFINED CPACK_PACKAGE_VERSION_MINOR)
        set(CPACK_PACKAGE_VERSION_MINOR ${PROJECT_VERSION_MINOR} PARENT_SCOPE)
        set(CPACK_PACKAGE_VERSION_MINOR ${PROJECT_VERSION_MINOR})
    endif()
    if(NOT DEFINED CPACK_PACKAGE_VERSION_PATCH)
        set(CPACK_PACKAGE_VERSION_PATCH ${PROJECT_VERSION_PATCH} PARENT_SCOPE)
        set(CPACK_PACKAGE_VERSION_PATCH ${PROJECT_VERSION_PATCH})
    endif()
    if(NOT DEFINED CPACK_PACKAGE_VERSION_TWEAK)
        set(CPACK_PACKAGE_VERSION_TWEAK ${PROJECT_VERSION_TWEAK} PARENT_SCOPE)
        set(CPACK_PACKAGE_VERSION_TWEAK ${PROJECT_VERSION_TWEAK})
    endif()
    if(NOT DEFINED CPACK_PACKAGE_VERSION)
        set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION} PARENT_SCOPE)
        set(CPACK_PACKAGE_VERSION ${PROJECT_VERSION})
    endif()

    #### Package name
    set(CPACK_PACKAGE_NAME ${PROJECT_NAME} PARENT_SCOPE)

    #### Package file name
    if(NOT ${COMPILED_ARCH} STREQUAL "all")
        if(MSVC)
            string(TOLOWER "msvc${MSVC_VERSION}" COMPILER_NAME)
        else()
            string(TOLOWER "${CMAKE_CXX_COMPILER_ID}${CMAKE_CXX_COMPILER_VERSION}" COMPILER_NAME)
        endif()
        string(REPLACE "." "" COMPILER_NAME "_${COMPILER_NAME}")
    endif()
    ## Debug build
    if(CMAKE_BUILD_TYPE STREQUAL "Debug")
        set(POSTFIX "_dbg")
    else()
        set(CPACK_STRIP_FILES TRUE PARENT_SCOPE)
    endif()
    ## Online installer
    if(CPACK_DOWNLOAD_ALL)
        set(POSTFIX "_online")
    endif()

    set(CPACK_PACKAGE_FILE_NAME
        "${CMAKE_PROJECT_NAME}_${CPACK_PACKAGE_VERSION}_${CMAKE_SYSTEM_NAME}${COMPILER_NAME}_${COMPILED_ARCH}${POSTFIX}"
        PARENT_SCOPE)

    #### Platform specific settings
    if(WIN32)
        set(CPACK_PACKAGE_INSTALL_DIRECTORY ${PROJECT_NAME} PARENT_SCOPE)
        string(REPLACE "_" "." CPACK_PACKAGE_VERSION ${CPACK_PACKAGE_VERSION})
        set(CPACK_PACKAGE_VERSION ${CPACK_PACKAGE_VERSION} PARENT_SCOPE)
    elseif(UNIX)
        set(CPACK_DEBIAN_PACKAGE_VERSION ${CPACK_PACKAGE_VERSION} PARENT_SCOPE)
        set(CPACK_DEBIAN_PACKAGE_ARCHITECTURE ${COMPILED_ARCH} PARENT_SCOPE)
    endif()

endfunction()
